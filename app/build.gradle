apply plugin: 'com.android.application'
apply plugin: 'kotlin-android'
apply plugin: 'kotlin-android-extensions'
apply plugin: 'kotlin-kapt'

android {
    compileSdkVersion 28
    buildToolsVersion "29.0.2"
    defaultConfig {
        applicationId "com.lodz.android.notifyreaddemo"
        minSdkVersion 22
        targetSdkVersion 28
        versionCode 2
        versionName "1.1"

        ndk {
            // 设置支持的SO库架构
            abiFilters 'armeabi' //, 'x86', 'armeabi-v7a', 'x86_64', 'arm64-v8a'
        }
    }


    def LOG_DEBUG = "LOG_DEBUG" //日志开关
    def APP_NAME = "APP_NAME"//应用名称
    def APP_TYPE = "APP_TYPE"//应用类型
    defaultConfig {
        buildConfigField "boolean", LOG_DEBUG, "true"
        buildConfigField "String", APP_NAME, "\"淘宝短信解析\""
        buildConfigField "int", APP_TYPE, "0"
    }

    // 程序在编译的时候会检查lint，有任何错误提示会停止build，我们可以关闭这个开关
    lintOptions {
        abortOnError false
        checkReleaseBuilds false
    }

    buildTypes {

        debug {//debug版本的配置
            minifyEnabled false // 是否进行混淆
            zipAlignEnabled false // 是否支持zip
            debuggable true // 是否支持调试
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }

        release {
//            buildConfigField "boolean", LOG_DEBUG, "false"
            minifyEnabled true // 是否进行混淆
            zipAlignEnabled true // 是否支持zip
            debuggable false // 是否支持调试
            jniDebuggable false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }
    }
    flavorDimensions 'Channel'
    productFlavors {
        TaobaoSms {
            dimension = 'Channel'
            applicationId = 'com.lodz.android.notifyreaddemo'
            versionCode = 3
            versionName = '1.2'
            buildConfigField "String", APP_NAME, "\"淘宝短信解析\""
            buildConfigField "int", APP_TYPE, "1"
            manifestPlaceholders = [
                    APP_NAME: "淘宝短信解析",
            ]
        }
        BankSms {
            dimension = 'Channel'
            applicationId = 'com.lodz.android.banksms'
            versionCode = 2
            versionName = '1.1'
            buildConfigField "String", APP_NAME, "\"银行短信解析\""
            buildConfigField "int", APP_TYPE, "2"
            manifestPlaceholders = [
                    APP_NAME: "银行短信解析",
            ]
        }
    }


    applicationVariants.all { variant ->
        variant.outputs.all { output ->
            def newName
            if (variant.baseName.endsWith('release')) {
                def formattedDate = new Date().format('yyyyMMddHHmm')
                newName = variant.getMergedFlavor().getBuildConfigFields().get("APP_NAME").value.replace("\"", "") + "-v" + variant.versionName + "-" + variant.baseName + "-" + formattedDate + ".apk"
            } else {
                newName = variant.getMergedFlavor().getBuildConfigFields().get("APP_NAME").value.replace("\"", "") + "-v" + variant.versionName + "-" + variant.baseName + ".apk"
            }
            outputFileName = newName
        }
    }
}

dependencies {
    // pandora
    implementation 'com.lodz:pandora:1.1.9'
    // bugly
    implementation 'com.tencent.bugly:crashreport:3.0.0'
    // 权限申请
    implementation 'org.permissionsdispatcher:permissionsdispatcher:4.5.0'
    kapt 'org.permissionsdispatcher:permissionsdispatcher-processor:4.5.0'
}
